/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_owen.skrit

	Author(s)	: 	Bartosz Kijanka, Chad Queen

	Purpose		: 	Just stand there and talk at anyone we're told to.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInit$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	m_Target$	= job$.GoalObject;

	SetState Face$;
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		if ( GameAuditor.GetDb.GetBool( "owen_fall_talked_to" ) )		
		{
			Report.generic( "Owen - Talked To.\n" );
			if ( m_Go$.GetStore().GetNumHires() == 2 )
			{		
				m_Go$.GetConversation.RSSelectConversation( "conversation_owen_fall_two_packmules" );
				TryAnim$('talk');
			}
			else if ( m_Go$.GetStore().GetNumHires() == 1 )
			{		
				m_Go$.GetConversation.RSSelectConversation( "conversation_owen_fall_one_packmule" );
				TryAnim$('talk');
			}	
			else 
			{			
				m_Go$.GetConversation.RSSelectConversation( "conversation_owen_fall_no_packmules" );
				TryAnim$('talk');
			}		
		}
		else
		{		
			m_Go$.GetConversation.RSSelectConversation( "conversation_owen_fall_first" );
			GameAuditor.GetDb.SetBool( "owen_fall_talked_to", true );
			TryAnim$('talk');
		}	

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}

