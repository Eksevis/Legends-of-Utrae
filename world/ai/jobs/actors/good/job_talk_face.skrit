//////////////////////////////////////////////////////////////////////////////////////
// File: job_talk_face.skrit
// This skrit causes an npc whom is moving to stop turn and face you,
// begin a talk fidget, and engage a conversation
// Author: Madigan
// This skrit (C)2002 Michael DeWitt, All Rights Reserved.
// Permission is granted for non-profit use.Please give credit to the author
// Commercial use without written permission is expressly prohibited.
//////////////////////////////////////////////////////////////////////////////////////
Go         m_Go$;
GoMind      m_Mind$;
GoBody     m_Body$;
Job         m_Job$;
Goid      m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

event OnJobInit$( Job job$ )
{
   m_Job$      = job$;
   m_Go$      = job$.Go;
   m_Mind$      = job$.Go.Mind;
   m_Body$      = job$.Go.Body;
   m_Target$   = job$.GoalObject;

   SetState Face$;
}

state Face$
{
   transition
   {
      -> Talk$:   OnWorldMessage( WE_MCP_FACING_LOCKEDON );
   }

event OnEnterState$
   {
      if( !m_Go$.HasConversation )
      {
         report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
                     m_Go$.GetTemplateName,
                     m_Go$.Goid );
         SetState Exiting$;
      }
      MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
   }
}

state Talk$
{
   event OnEnterState$
   {
      m_Go$.GetConversation.RSActivateDialogue();
      SetState Fidget$;
   }
}
////////////////////////////////////////
//   fidget while we're talking
state Fidget$
{
   transition
   {
      -> Exiting$:      OnWorldMessage( WE_REQ_TALK_END );
      -> Fidget$:         OnWorldMessage( WE_ANIM_DONE );
   }

   event OnEnterState$
   {
      report.generic( "\n\n\n******************* starting fidget\n\n\n" );
         if( m_Go$.Mind.ActorAutoFidgets )
         {
            StartFidgetIfRequired$();
         }
   }
}
state Exiting$
{
   event OnEnterState$
   {
      MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

      m_Job$.MarkForDeletion();
   }
}