/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_sanctuary_keeper.skrit

	Author(s)	: 	David Tomandl, Eric Tams, Chad Queen, Bartosz Kijanka

	Purpose		: 	Custom talk job for the multiplayer-world NPC by the
					flooded sanctuary.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on coffee... and you're upset about your dungeon
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
		-> Exiting$:	OnWorldMessage( WE_ANIM_DONE ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}

	event OnEnterState$
	{
		if Victory.IsQuestActive( "quest_goblin_quest" )
		{
			if( !GameAuditor.GetDb.GetBool( "quest_goblin_plans" ) )
			{
				if( m_Target$.IsValid() ) 
				{	
					GameAuditor.GetDb.SetBool( "quest_goblin_plans", true );
					
					SiegePos SpawnPos$;
					
					GoCloneReq cloneReq$ = MakeGoCloneReq( "minigun_napalm_quest" );
					
					SpawnPos$ = m_Target$.go.placement.position;
					cloneReq$.StartingPos	= SpawnPos$;
				    
					If( m_Target$.Go.Hasmind )
					{
						if( AIQuery.FindSpotRelativeToSource( m_Target$.Go, 1, 1.5, 2.0, m_Target$.Go.Mind.TempPos1 ) )
				   		{
				   			cloneReq$.StartingPos = m_Target$.Go.Mind.TempPos1;
				   		}
				   	}	
					cloneReq$.SnapToTerrain = true;
			        GoDb.SCloneGo( cloneReq$ );
				}
			}
			m_Go$.GetConversation.RSSelectConversation( "conversation_goblin_quest_complete" );
			Victory.RSCompletedQuest( "quest_goblin_quest", m_Target$ );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "turn_world_red" ))
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_goblin_quest_red" );
		}
		else if ( Victory.IsQuestCompleted( "quest_goblin_quest" ))
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_goblin_quest_complete" );
		}
		else
		{
			m_Go$.GetConversation.RSSelectConversation( "conversation_goblin_quest" );
		}
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
